本章介绍 Groovy 闭包。 Groovy 中的闭包是一个开放的、匿名的代码块，可以接受参数、返回值和分配给变量。闭包可以引用在其周围范围内声明的变量。与闭包的正式定义相反，Groovy 语言中的 `Closure` 还可以包含在其周围范围之外定义的自由变量。虽然打破了闭包的正式概念，但它提供了本章中描述的各种优点。

# 语法

## 定义闭包

闭包定义遵循以下语法：

    { [closureParameters -> ] statements }

其中 `[closureParameters->]` 是可选的以逗号分隔的参数列表，这些参数类似于方法参数列表，并且这些参数可以是类型化的或非类型化的。

statements是 0 个或多个 Groovy 语句。

当指定参数列表时， `->` 字符是必需的，用于将参数与闭包主体分隔开。*statements* 部分由 0、1 或多个 Groovy 语句组成。

下面是一些合法的闭包定义:

    { item++ }                                          
    
    { -> item++ }                                       
    
    { println it }                                      
    
    { it -> println it }                                
    
    { name -> println name }                            
    
    { String x, int y ->                                
        println "hey ${x} the value is ${y}"
    }
    
    { reader ->                                         
        def line = reader.readLine()
        line.trim()
    }

-   该闭包引用了名为 `item` 的变量

-   可以通过添加箭头 (`->`) 将闭包参数与代码显式分离

-   该闭包使用了隐式参数 (`it`)

-   另一种版本，其中 `it` 是显式参数

-   在这种情况下，通常最好为参数使用显式名称

-   接受两个类型参数的闭包

-   一个闭包可以包含多个语句

## 闭包作为对象

闭包是 `groovy.lang.Closure` 类的实例，可以像任何其他变量一样分配给变量或字段，尽管它是一个代码块：

    def listener = { e -> println "Clicked on $e.source" }      
    assert listener instanceof Closure
    Closure callback = { println 'Done!' }                      
    Closure<Boolean> isTextFile = {
        File it -> it.name.endsWith('.txt')                     
    }

-   您可以将闭包分配给变量，它是 `groovy.lang.Closure` 的实例

-   如果不使用 `def` 或 `var` ，可以使用 `groovy.lang.Closure` 作为类型

-   或者，您可以使用 `groovy.lang.Closure` 的泛型类型指定闭包的返回类型

## 调用闭包

闭包作为匿名代码块，可以像任何其他方法一样被调用。如果你定义一个不带参数的闭包，如下所示：

    def code = { 123 }

可以像方法一样通过变量来调用闭包, 只有在调用闭包时才会执行闭包内的代码:

    assert code() == 123

或者，您可以明确地使用 call 方法：

    assert code.call() == 123

如果闭包接受参数，原理是相同的：

    def isOdd = { int i -> i%2 != 0 }                           
    assert isOdd(3) == true                                     
    assert isOdd.call(2) == false                               
    
    def isEven = { it%2 == 0 }                                  
    assert isEven(3) == false                                   
    assert isEven.call(2) == true                               

-   定义一个接受 `int` 作为参数的闭包

-   可以直接调用

-   或使用 `call` 方法

-   对于带有隐式参数 (`it`) 的闭包也是如此

-   可以直接使用 `(arg)` 调用

-   或使用 `call`

与方法不同，闭包在调用时 **总是** 返回一个值。下一节讨论如何声明闭包参数、何时使用它们以及什么是[隐式 \"it\" 参数](#implicit-it)。

# 参数

## 普通参数

闭包的参数遵循与常规方法的参数相同的原则：

-   可选类型

-   具有一个名字

-   具有可选的默认值

参数之间用逗号分隔：

    def closureWithOneArg = { str -> str.toUpperCase() }
    assert closureWithOneArg('groovy') == 'GROOVY'
    
    def closureWithOneArgAndExplicitType = { String str -> str.toUpperCase() }
    assert closureWithOneArgAndExplicitType('groovy') == 'GROOVY'
    
    def closureWithTwoArgs = { a,b -> a+b }
    assert closureWithTwoArgs(1,2) == 3
    
    def closureWithTwoArgsAndExplicitTypes = { int a, int b -> a+b }
    assert closureWithTwoArgsAndExplicitTypes(1,2) == 3
    
    def closureWithTwoArgsAndOptionalTypes = { a, int b -> a+b }
    assert closureWithTwoArgsAndOptionalTypes(1,2) == 3
    
    def closureWithTwoArgAndDefaultValue = { int a, int b=2 -> a+b }
    assert closureWithTwoArgAndDefaultValue(1) == 3

## 隐式参数

当闭包未显式定义参数列表（使用 `->` ）时，闭包 **始终** 定义一个隐式参数，名为 `it` 。这意味着这段代码：

    def greeting = { "Hello, $it!" }
    assert greeting('Patrick') == 'Hello, Patrick!'

与此严格等效：

    def greeting = { it -> "Hello, $it!" }
    assert greeting('Patrick') == 'Hello, Patrick!'

如果你想声明一个不接受参数的闭包，并且必须限制为不带参数的调用，那么你 **必须** 使用显式的空参数列表来声明它：

    def magicNumber = { -> 42 }
    
    // this call will fail because the closure doesn't accept any argument
    magicNumber(11)

## 可变参数

闭包可以像任何其他方法一样声明变量参数。 如果最后一个参数具有可变长度, 或者是数组 , 那么闭包可以接受可变数量参数，如下例所示：

    def concat1 = { String... args -> args.join('') }           
    assert concat1('abc','def') == 'abcdef'                     
    def concat2 = { String[] args -> args.join('') }            
    assert concat2('abc', 'def') == 'abcdef'
    
    def multiConcat = { int n, String... args ->                
        args.join('')*n
    }
    assert multiConcat(2, 'abc','def') == 'abcdefabcdef'

-   接受可变数量的字符串作为第一个参数

-   可以使用任意数量的参数调用它，而 **不必** 将它们显式包装到数组中

-   如果 *args* 参数声明为数组，则可以直接使用相同的行为

-   只要 **最后一个** 参数是数组或显式 可变长度 类型

# 委托(Delegation)策略

## Groovy 闭包与 lambda 表达式

Groovy 将闭包定义为 [Closure类的实例](#closure-as-object)。它与 [lambda expressions in Java 8](https://docs.oracle.com/javase/tutorial/java/javaOO/lambdaexpressions.html) 非常不同。委托是 Groovy 闭包中的一个关键概念，而 lambda 中没有等效概念。*更改委托* 或 *更改闭包的委托策略* 的能力使得在 Groovy 中设计漂亮的领域特定语言 (DSL) 成为可能。

## Owner, delegate 和 this

要理解delegate的概念，我们首先要解释一下闭包中 `this` 的含义。闭包实际上定义了 3 个不同的东西：

-   `this` 对应于定义闭包的 *封闭类*

-   `owner` 对应于定义闭包的 *封闭对象* ，可以是类，也可以是闭包

-   `delegate` 对应于第三方对象, 当闭包的调用者未定义时，方法调用或属性将被解析到 `delegate`。

### 闭包中的this

在闭包中，调用 `getThisObject` 将返回定义闭包的封闭类。它相当于使用显式 `this` ：

    class Enclosing {
        void run() {
            def whatIsThisObject = { getThisObject() }          
            assert whatIsThisObject() == this                   
            def whatIsThis = { this }                           
            assert whatIsThis() == this                         
        }
    }
    class EnclosedInInnerClass {
        class Inner {
            Closure cl = { this }                               
        }
        void run() {
            def inner = new Inner()
            assert inner.cl() == inner                          
        }
    }
    class NestedClosures {
        void run() {
            def nestedClosures = {
                def cl = { this }                               
                cl()
            }
            assert nestedClosures() == this                     
        }
    }

-   在 `Enclosing` 类中定义，并返回 `getThisObject`

-   调用闭包将返回定义闭包的 `Enclosing` 实例

-   一般来说，只会使用快捷方式 `this`

-   它返回 **完全** 相同的对象

-   如果闭包是在内部类中定义的

-   闭包中的 `this` 将返回内部类，而不是顶级类

-   在嵌套闭包的情况下，就像这里 `cl` 被定义在 `nestedClosures` 范围内一样

-   那么 `this` 对应于最接近的外部类，而不是封闭的闭包！

当然可以通过这种方式从封闭类中调用方法：

    class Person {
        String name
        int age
        String toString() { "$name is $age years old" }
    
        String dump() {
            def cl = {
                String msg = this.toString()               
                println msg
                msg
            }
            cl()
        }
    }
    def p = new Person(name:'Janice', age:74)
    assert p.dump() == 'Janice is 74 years old'

-   闭包调用 `this` 上的 `toString` ，这实际上会调用封闭对象（即 `Person` 实例）上的 `toString` 方法

### 闭包的Owner属性

闭包的 `owner` 与 [闭包中的this](#closure-this) 的定义非常相似，但有一个细微的区别：它将返回直接封闭的对象，无论是闭包还是类：

    class Enclosing {
        void run() {
            def whatIsOwnerMethod = { getOwner() }               
            assert whatIsOwnerMethod() == this                   
            def whatIsOwner = { owner }                          
            assert whatIsOwner() == this                         
        }
    }
    class EnclosedInInnerClass {
        class Inner {
            Closure cl = { owner }                               
        }
        void run() {
            def inner = new Inner()
            assert inner.cl() == inner                           
        }
    }
    class NestedClosures {
        void run() {
            def nestedClosures = {
                def cl = { owner }                               
                cl()
            }
            assert nestedClosures() == nestedClosures            
        }
    }

-   在 `Enclosing` 类中定义，并返回 `getOwner`

-   调用闭包将返回定义闭包的 `Enclosing` 实例

-   一般来说，您只想使用快捷方式 `owner` 表示法

-   它返回 **完全** 相同的对象

-   如果闭包是在内部类中定义的

-   闭包中的 `owner` 将返回 *内部类*，而不是顶级类

-   但在嵌套闭包的情况下，就像这里 `cl` 被定义在 `nestedClosures` 范围内一样

-   那么 `owner` 对应于封闭的闭包，与 `this` 是不同的对象！

### 闭包的Delegate

可以使用 `delegate` 属性或调用 `getDelegate` 方法来访问闭包的委托。它是在 Groovy 中构建领域特定语言的强大概念。虽然 [this](#closure-this) 和 [owner](#closure-owner) 指的是闭包的词法范围，但 `delegate` 是闭包将使用的用户定义的对象。默认情况下，`delegate` 设置为 \`owner \`：

    class Enclosing {
        void run() {
            def cl = { getDelegate() }                          
            def cl2 = { delegate }                              
            assert cl() == cl2()                                
            assert cl() == this                                 
            def enclosed = {
                { -> delegate }.call()                          
            }
            assert enclosed() == enclosed                       
        }
    }

-   您可以调用 `getDelegate` 方法获取闭包的委托

-   或使用 `delegate` 属性

-   两者都返回相同的对象

-   这是封闭类或闭包

-   特别是在嵌套闭包的情况下

-   `delegate` 将对应于 `owner`

闭包的委托可以更改为 **任何对象**。让我们通过创建两个类来说明这一点，这两个类不是彼此的子类，但都定义了一个名为 `name` 的属性：

    class Person {
        String name
    }
    class Thing {
        String name
    }
    
    def p = new Person(name: 'Norman')
    def t = new Thing(name: 'Teapot')

然后我们定义一个闭包来获取委托上的 `name` 属性：

    def upperCasedName = { delegate.name.toUpperCase() }

然后通过改变闭包的委托，可以看到目标对象发生了变化：

    upperCasedName.delegate = p
    assert upperCasedName() == 'NORMAN'
    upperCasedName.delegate = t
    assert upperCasedName() == 'TEAPOT'

此时，行为与在闭包的词法作用域中定义 `target` 变量没有什么不同：

    def target = p
    def upperCasedNameUsingVar = { target.name.toUpperCase() }
    assert upperCasedNameUsingVar() == 'NORMAN'

但是，他们存在重大差异：

-   在最后一个示例中，`target` 是从闭包内引用的局部变量

-   委托可以透明地使用，也就是说，无需在方法调用前添加 `delegate.` 前缀，如下一段所述。

### Delegation策略

每当在闭包中，在没有显式设置调用者的情况下访问属性时，就会涉及委托策略：

    class Person {
        String name
    }
    def p = new Person(name:'Igor')
    def cl = { name.toUpperCase() }                 
    cl.delegate = p                                 
    assert cl() == 'IGOR'                           

-   `name` 没有引用闭包词法范围内的变量

-   我们可以将闭包的委托更改为 `Person` 的实例

-   方法调用将会成功

此代码起作用的原因是 `name` 属性将在 `delegate` 对象上解析！这是解决闭包内的属性或方法调用的一种非常强大的方法。不需要设置显式的 `delegate.` , 即可进行调用，因为闭包的默认委托策略使得它如此。闭包实际上定义了多种您可以选择的解决策略：

-   `Closure.OWNER_FIRST` 是 **默认策略**。如果 **owner** 存在属性/方法，则将调用它。如果没有，则使用\`delegate\`。

-   `Closure.DELEGATE_FIRST` 首先使用 **delegate**，然后使用 **owner**

-   `Closure.OWNER_ONLY` 只会解析 **owner** 的属性/方法查找, **delegate** 将被忽略。

-   `Closure.DELEGATE_ONLY` 将仅解析 **delegate** 上的属性/方法查找： **owner** 将被忽略。

-   `Closure.TO_SELF` 可供需要高级元编程技术并希望实现自定义解析策略的开发人员使用：解析不会针对 `owner` 或 `delegate` 进行，而仅针对闭包类本身进行。仅当您实现自己的 `Closure` 子类时才有意义。

让我们用以下代码来说明默认的"owner first"策略：

    class Person {
        String name
        def pretty = { "My name is $name" }             
        String toString() {
            pretty()
        }
    }
    class Thing {
        String name                                     
    }
    
    def p = new Person(name: 'Sarah')
    def t = new Thing(name: 'Teapot')
    
    assert p.toString() == 'My name is Sarah'           
    p.pretty.delegate = t                               
    assert p.toString() == 'My name is Sarah'           

-   为了便于说明，我们定义了一个引用 `name` 的闭包成员

-   `Person` 和 `Thing` 类都定义了 `name` 属性

-   使用默认策略， `name` 属性首先在 `owner` 上解析

-   因此，如果我们将 `delegate` 更改为 `t` ，它是 `Thing` 的实例

-   结果没有变化： `name` 首先在闭包的 `owner` 上解析

然而，可以改变闭包的解析策略：

    p.pretty.resolveStrategy = Closure.DELEGATE_FIRST
    assert p.toString() == 'My name is Teapot'

通过更改 `resolveStrategy` ，Groovy 会解析 \"隐式 this\" 引用的方式：在这种情况下，将首先在delegate中查找 `name` ，如果没有找到，在owner上查找。由于 `name` 在 `delegate(Thing 的实例)` 中定义的了, 因此使用该值。

\"delegate first\" 和 \"delegate only\" 或者 \"owner first\" 和 \"owner only\" 之间的区别可以通过如下来说明:

    class Person {
        String name
        int age
        def fetchAge = { age }
    }
    class Thing {
        String name
    }
    
    def p = new Person(name:'Jessica', age:42)
    def t = new Thing(name:'Printer')
    def cl = p.fetchAge
    cl.delegate = p
    assert cl() == 42          
    cl.delegate = t
    assert cl() == 42          
    
    cl.resolveStrategy = Closure.DELEGATE_ONLY
    cl.delegate = p
    assert cl() == 42          
    cl.delegate = t
    try {
        cl()                   
        assert false
    } catch (MissingPropertyException ex) {
        // "age" is not defined on the delegate
    }

-   对于 \"owner first\" ，delegate是什么都无所谓

-   对于 \"delegate only\", `p` 作为delegate可以成功

-   对于 \"delegate only\" , `t` 作为 delegate 会失败

在此示例中，我们定义了两个类，它们都具有 `name` 属性，但只有 `Person` 类声明了 `age` 。 `Person` 类还声明了一个引用 `age` 的闭包。我们可以将默认的解析策略从 \"owner first\" 更改为 \"delegate only\"。由于闭包的owner是 `Person` 类，因此我们可以检查delegate是否是 `Person` 的实例，则调用闭包是成功的，但是如果 delegate 是 `Thing` 的实例，它失败并返回 `groovy.lang.MissingPropertyException` 。尽管闭包是在 `Person` 类中定义的，但不使用owner。

有关如何使用此功能来开发 DSL 的全面说明可以在手册的 [dedicated section of the manual](../core-domain-specific-languages.html). 中找到。

### Delegation策略 in the presence of metaprogramming

在描述 \"owner first\" 委托策略时，我们谈到使用owner的属性/方法（如果它 *存在* ），否则使用delegate的相应属性/方法。还有一个类似的但与其相反 \"delegate first\" 的策略。与其使用 *存在* 一词，不如使用 *处理* 一词更为准确。这意味着对于 \"owner first\"，如果属性/方法存在于owner中，或者它具有 propertyMissing/methodMissing 钩子函数，则owner将处理成员访问。

我们可以通过对上一个示例进行稍微修改的版本来看到这一点：

    class Person {
        String name
        int age
        def fetchAge = { age }
    }
    class Thing {
        String name
        def propertyMissing(String name) { -1 }
    }
    
    def p = new Person(name:'Jessica', age:42)
    def t = new Thing(name:'Printer')
    def cl = p.fetchAge
    cl.resolveStrategy = Closure.DELEGATE_FIRST
    cl.delegate = p
    assert cl() == 42
    cl.delegate = t
    assert cl() == -1

在此示例中，即使我们的 `Thing` 类实例（cl的最后一次使用中的delegate）没有 `age` 属性，但它通过其 `propertyMissing` 钩子函数处理丢失的属性, 意味着 `age` 将是 `-1`。

# GString 中的闭包

以下代码：

    def x = 1
    def gs = "x = ${x}"
    assert gs == 'x = 1'

该代码的行为正如您所期望的，但如果添加以下内容会发生什么：

    x = 2
    assert gs == 'x = 2'

你会看到断言失败了！有两个原因：

-   GString 仅延迟计算表达式的 `toString` 形式

-   GString 中的语法 `${x}` 并 **不** 代表闭包，而是 `$x` 的表达式，这会在在创建 GString 时进行评估.

在我们的示例中， `GString` 是通过一个引用 `x` 的表达式来创建的。创建 GString 时， `x` 的值为1，so the `GString` is created with a value of 1。当触发断言时，通过使用 `toString` 将 `1` 转换为 `String` 。当我们将 `x` 更改为 `2` 时，我们确实更改了 `x` 的值，但它是另外一个对象，并且 GString 仍然引用旧的对象(1)。

`GString` 只有当他 *引用的值* 是可变的时, `GString` 的 `toString` 形式才会改变。如果只是引用发生变化，则不会发生任何事情。

可以理解为将 `x` 作为参数传递到了 `GString` 中, 只有 `x` 引用的对象发生了改变, `GString` 才会改变. 如果将其他值赋给 `x` , 对 `GString` 没有任何影响

如果您需要在 GString 中使用真正的闭包，例如强制执行变量的惰性求值，则需要使用替代语法 `${→ x}` ，如下所示：

    def x = 1
    def gs = "x = ${-> x}"
    assert gs == 'x = 1'
    
    x = 2
    assert gs == 'x = 2'

让我们用这段代码来说明它与值可变有何不同：

    class Person {
        String name
        String toString() { name }          
    }
    def sam = new Person(name:'Sam')        
    def lucy = new Person(name:'Lucy')      
    def p = sam                             
    def gs = "Name: ${p}"                   
    assert gs == 'Name: Sam'                
    p = lucy                                
    assert gs == 'Name: Sam'                
    sam.name = 'Lucy'                       
    assert gs == 'Name: Lucy'               

-   `Person` 类有一个 `toString` 方法，返回 `name` 属性

-   我们创建了第一个名为 *Sam* 的 `Person`

-   我们创建另一个名为 *Lucy* 的 `Person`

-   `p` 变量设置为 `Sam`

-   创建了一个闭包，引用 `p` 的值，也就是说 *Sam*

-   所以当我们计算字符串时，它返回 *Sam*

-   如果我们将 p 更改为 *Lucy*

-   该字符串的计算结果仍为 *Sam* ，因为它是创建 `GString` 时 `p` 的值

-   所以如果我们对 *Sam* 进行修改，将名字改为 *Lucy*

-   这次 `GString` 已正确改变

因此，如果您不想依赖可变对象或包装对象，则必须在 `GString` 通过显式声明空参数列表来使用闭包的方式：

    class Person {
        String name
        String toString() { name }
    }
    def sam = new Person(name:'Sam')
    def lucy = new Person(name:'Lucy')
    def p = sam
    // Create a GString with lazy evaluation of "p"
    def gs = "Name: ${-> p}"
    assert gs == 'Name: Sam'
    p = lucy
    assert gs == 'Name: Lucy'

# 闭包强制转换

闭包可以转换为接口或单一抽象方法类型。请参阅手册的[这一部分](core-semantics.html#closure-coercion)以获取完整的说明。

# 函数式编程

闭包是 Groovy 中函数式编程范例的核心。函数上的一些函数式编程操作可直接在 `Closure` 类上使用，如本节中所示。

## 柯里化

在 Groovy 中，柯里化指的是部分应用的概念。由于 Groovy 对闭包应用了不同的作用域规则，因此它与函数式编程中的柯里化的真正概念并不相符。 Groovy 中的柯里化将允许您设置闭包的一个参数，并且它将返回一个接受少一个参数的新闭包。

### 左柯里化

左柯里化是指设置闭包最左边的参数，如下例所示：

    def nCopies = { int n, String str -> str*n }    
    def twice = nCopies.curry(2)                    
    assert twice('bla') == 'blabla'                 
    assert twice('bla') == nCopies(2, 'bla')        

-   `nCopies` 闭包定义了两个参数

-   `curry` 会将第一个参数设置为 2 ，创建一个接受单个 `String` 的新闭包（函数）

-   因此新函数调用只需使用 `String` 即可调用

-   等于调用带有两个参数的 `nCopies`

### 右柯里化

与左柯里化类似，可以设置闭包最右边的参数：

    def nCopies = { int n, String str -> str*n }    
    def blah = nCopies.rcurry('bla')                
    assert blah(2) == 'blabla'                      
    assert blah(2) == nCopies(2, 'bla')             

-   `nCopies` 闭包定义了两个参数

-   `rcurry` 将最后一个参数设置为 `bla` ，创建一个接受单个 `int` 的新闭包（函数）

-   因此新函数调用只需使用一个 `int` 即可调用

-   相当于调用带有两个参数的 `nCopies`

### 基于索引的柯里化

如果闭包接受超过 2 个参数，则可以使用 `ncurry` 设置任意参数：

    def volume = { double l, double w, double h -> l*w*h }      
    def fixedWidthVolume = volume.ncurry(1, 2d)                 
    assert volume(3d, 2d, 4d) == fixedWidthVolume(3d, 4d)       
    def fixedWidthAndHeight = volume.ncurry(1, 2d, 4d)          
    assert volume(3d, 2d, 4d) == fixedWidthAndHeight(3d)        

-   `volume` 函数定义了3个参数

-   `ncurry` 会将第二个参数 (index = 1) 设置为 `2d` ，创建一个接受l和h的新体积函数

-   该函数相当于调用省略了w的 `volume`

-   还可以设置多个参数，从指定索引开始

-   结果函数接受的参数数量与初始参数减去 `ncurry` 设置的参数数量相同

## 记忆化

记忆化允许缓存闭包调用的结果。如果函数（闭包）完成的计算很慢，但您知道该函数将经常使用相同的参数调用，那么这很有趣。一个典型的例子是斐波那契套件。一个简单的实现可能如下所示：

    def fib
    fib = { long n -> n<2?n:fib(n-1)+fib(n-2) }
    assert fib(15) == 610 // slow!

这是一个幼稚的实现，因为"fib"经常使用相同的参数递归调用，从而导致指数算法：

-   计算 fib(15) 需要 fib(14) 和 fib(13) 的结果

-   计算 fib(14) 需要 fib(13) 和 fib(12) 的结果

由于调用是递归的，您已经可以看到我们将一次又一次地计算相同的值，尽管它们可以被缓存。这种简单的实现可以通过使用 `memoize` 缓存调用结果来"修复"：

    fib = { long n -> n<2?n:fib(n-1)+fib(n-2) }.memoize()
    assert fib(25) == 75025 // fast!

The cache works **using the actual values of the arguments**. 这意味着如果您将记忆化与原始或装箱原始类型以外的其他类型一起使用，则应该非常小心。

可以使用替代方法调整缓存的行为：

-   `memoizeAtMost` 将生成一个新的闭包，最多缓存 n 个值

-   `memoizeAtLeast` 将生成一个新的闭包，该闭包缓存至少 n 个值

-   `memoizeBetween` will generate a new closure which caches at least n values and at most n values

所有 memoize 变体中使用的缓存都是 LRU 缓存。

## 闭包组合

闭包组合对应于函数组合的概念(通过组合两个或多个函数（链式调用）)来创建一个新函数，如下例所示：

    def plus2  = { it + 2 }
    def times3 = { it * 3 }
    
    def times3plus2 = plus2 << times3
    assert times3plus2(3) == 11
    assert times3plus2(4) == plus2(times3(4))
    
    def plus2times3 = times3 << plus2
    assert plus2times3(3) == 15
    assert plus2times3(5) == times3(plus2(5))
    
    // reverse composition
    assert times3plus2(3) == (times3 >> plus2)(3)

## Trampoline

递归算法通常受到物理限制的限制：最大堆栈高度。例如，如果您调用一个递归调用自身太深的方法，您最终将收到一个 `StackOverflowException` 。

在这些情况下有帮助的方法是使用 `Closure` 及其 `Trampoline` 功能。

Closures are wrapped in a `TrampolineClosure`. Upon calling, a trampolined `Closure` will call the original `Closure` waiting for its result. If the outcome of the call is another instance of a `TrampolineClosure`, created perhaps as a result to a call to the `trampoline()` method, the `Closure` will again be invoked. This repetitive invocation of returned trampolined Closures instances will continue until a value other than a trampolined `Closure` is returned. That value will become the final result of the trampoline. That way, calls are made serially, rather than filling the stack.

下面是使用 trampoline() 实现阶乘函数的示例：

    def factorial
    factorial = { int n, def accu = 1G ->
        if (n < 2) return accu
        factorial.trampoline(n - 1, n * accu)
    }
    factorial = factorial.trampoline()
    
    assert factorial(1)    == 1
    assert factorial(3)    == 1 * 2 * 3
    assert factorial(1000) // == 402387260.. plus another 2560 digits

## 方法指针

能够使用常规方法作为闭包通常很实用。例如，您可能想要使用闭包的柯里化功能，但这些功能不适用于普通方法。在 Groovy 中，您可以使用[方法指针操作符](core-operators.html#method-pointer-operator)从任何方法获取闭包。
