本章将覆盖Groovy编程语言的语法。 Groovy语言的语法是由JAVA语言的语法衍生出来, 但具体结构上会有所增强，同时容许一定的简化。



# 关键字

下面的列表列出了全部Groovy语言的保留关键字:

保留关键字

----------------- ----------------- ----------------- -----------------
  abstract          assert            break             case

  catch             class             const             continue

  def               default           do                else

  enum              extends           final             finally

  for               goto              if                implements

  import            instanceof        interface         native

  new               null              non-sealed        package

  public            protected         private           return

  static            strictfp          super             switch

  synchronized      this              threadsafe        throw

  throws            transient         try               while
----------------- ----------------- ----------------- -----------------

  : 保留关键字

其中，`const` 、`goto` 、`strictfp` 和 `threadsafe` 目前没有在使用中。

一般情况下，保留关键字不能用作变量、字段和方法名。

将方法名用引号括起来, 允许定义具有与关键字相同名称的方法, 如下所示:

    // reserved keywords can be used for method names if quoted
    def "abstract"() { true }
    // when calling such methods, the name must be qualified using "this."
    this.abstract()

使用这样的名称可能会令人困惑，通常最好避免使用。 这个技巧主要是为了在特定的Java集成场景和特定[DSL](../core-domain-specific-languages.html) 场景实现一些希望使用与关键字相同名称的"动词"和"名词"的情况。

此外，Groovy 还具有以下上下文关键字：

Contextual Keywords

----------------- ----------------- ----------------- -----------------
  as                in                permits           record

  sealed            trait             var               yields
----------------- ----------------- ----------------- -----------------

  : Contextual Keywords

这些单词只在特定上下文中成为关键字，并且在某些地方可以更自由地使用，特别是在变量、字段和方法名中。

这些额外的宽松性允许我们使用在较早版本的Groovy中不是关键字，或者在Java中也不是关键字的单词来作为变量名, 示例如下:

    // contextual keywords can be used for field and variable names
    def as = true
    assert as
    
    // contextual keywords can be used for method names
    def in() { true }
    // when calling such methods, the name only needs to be qualified using "this." in scenarios which would be ambiguous
    this.in()

熟悉这些上下文关键字的Groovy程序员可能仍然希望避免使用这些名称，除非有充分的理由要使用这样的名称。

对于保留关键字的限制也适用于原始类型、布尔字面量和空字面量（这些稍后会进行讨论）：

其他保留关键字

----------------- ----------------- ----------------- -----------------
  null              true              false             boolean

  char              byte              short             int

  long              float             double            
----------------- ----------------- ----------------- -----------------

  : 其他保留关键字

虽然不建议，但可以使用与保留关键字相同的技巧：

    def "null"() { true }  // not recommended; potentially confusing
    assert this.null()     // must be qualified

将这些单词用作方法名可能会引起混淆，通常最好避免使用。 然而，对于某些 [DSLs](../core-domain-specific-languages.html) 来说，这可能是有用的。

# 标识符

## 普通标识符

标识符以字母，美元符或下划线开头。不能以数字开头

字母可以是以下的范围:

-   \'a\' to \'z\' (小写 ascii 字母)

-   \'A\' to \'Z\' (大写 ascii 字母)

-   \'\\u00C0\' to \'\\u00D6\'

-   \'\\u00D8\' to \'\\u00F6\'

-   \'\\u00F8\' to \'\\u00FF\'

-   \'\\u0100\' to \'\\uFFFE\'

之后的字符可以包含字母和数字。

以下是一些有效标识符的示例（变量名）：

    def name
    def item3
    def with_underscore
    def $dollarStart

但以下的是不合法的标识符:

    def 3tier
    def a+b
    def a#b

所有跟在点号后的关键字也是合法的标识符:

    foo.as
    foo.assert
    foo.break
    foo.case
    foo.catch

## 带引号的标识符

带引号标识符出现在点号表达式的点号之后. 例如，`person.name` 表达式中 `name` 部分就可以通过引号引起来成为 `person."name"` 或 `person.'name'` . 有趣的是，当某些标识符包含Java语言规范中禁止的非法字符，引号引起来后在Groovy就会被容许。 例如，像一个破折号，空格，感叹号等字符。

    def map = [:]
    
    map."an identifier with a space and double quotes" = "ALLOWED"
    map.'with-dash-signs-and-single-quotes' = "ALLOWED"
    
    assert map."an identifier with a space and double quotes" == "ALLOWED"
    assert map.'with-dash-signs-and-single-quotes' == "ALLOWED"

正如我们在 [strings](#all-strings) 中看到的, Groovy提供不同的字符串字面量。 所有跟着点号的字符串都是被允许的：

    map.'single quote'
    map."double quote"
    map.'''triple single quote'''
    map."""triple double quote"""
    map./slashy string/
    map.$/dollar slashy string/$

正如下面的例子，纯字符的字符串和Groovy的GStings（字符串插值）的区别是，计算整个标识符时，插入的字符值是被插入到最终字符串:

    def firstname = "Homer"
    map."Simpson-${firstname}" = "Homer Simpson"
    
    assert map.'Simpson-Homer' == "Homer Simpson"

# 字符串

字符以链的形式形成的文本被称作字符串。 Groovy容许你实例化 `java.lang.String` 对象，以及在其他语言中被称作 *插值字符串* （interpolated strings）的GStrings (`groovy.lang.GString`)

## 单引号字符串

单引号字符串是一系列被单引号引起来的字符：

    'a single-quoted string'

单引号括起的字符串是普通的 `java.lang.String` ，不支持插值。

## 字符串连接

所有Groovy的字符串都可以用 `+` 号连接起来：

    assert 'ab' == 'a' + 'b'

## 三单引号字符串

三引号字符串是一系列被三个单引号引起来的字符：

    '''a triple-single-quoted string'''

单引号括起的字符串是普通的 `java.lang.String` ，不支持插值。

三引号字符串可以是多行的。 你不必将字符串分割成几块，也不必用连接符或换行符转义字符来将字符串跨行：:

    def aMultilineString = '''line one
    line two
    line three'''

如果你的代码是缩进的，比如在类里面的方法，你的字符串将会包含这些空白的缩进。 Groovy开发套件提供 `String#stripIndent()` 方法来剥离（stripping out）缩进， 同时Groovy开发套件提供 `String#stripMargin()` 方法，该方法需要一个分隔符来识别从字符串的开头开始删除的文本。

当创建如下的字符串时:

    def startingAndEndingWithANewline = '''
    line one
    line two
    line three
    '''

你会发现，得到的是一个以换行符开头的字符串。可以通过反斜杠转义换行来剥离该字符：

    def strippedFirstNewline = '''\
    line one
    line two
    line three
    '''
    
    assert !strippedFirstNewline.startsWith('\n')

### 转义特殊字符串

你可以用反斜杠来转义单引号来避免终止字符串：

    'an escaped single quote: \' needs a backslash'

同时你可以用两个反斜杠转义转义符号本身：

    'an escaped escape character: \\ needs a double backslash'

一些特殊的字符串都是用反斜杠来作为转义字符：

--------------------------------------------------------------------------------------------------------------------------------------
  Escape sequence         Character
----------------------- --------------------------------------------------------------------------------------------------------------
  \\b                     backspace

  \\f                     formfeed

  \\n                     newline

  \\r                     carriage return

  \\s                     single space

  \\t                     tabulation

  \\\\                    backslash

  \\\'                    single quote within a single-quoted string (and optional for triple-single-quoted and double-quoted strings)

  \\\"                    double quote within a double-quoted string (and optional for triple-double-quoted and single-quoted strings)
  --------------------------------------------------------------------------------------------------------------------------------------

当涉及到后面讨论的其他类型的字符串时，我们将看到一些更多的转义细节。

### Unicode转义序列

对于不存在你键盘上的字符，你可以使用Unicode转义序列：反斜杠，然后是u，然后4个十六进制数字。

例如，欧元货币符号可以表示为：

    'The Euro currency symbol: \u20AC'

## 双引号字符串

双引号字符串是一系列被双引号引起来的字符：

    "a double-quoted string"

如果没有插值表示式的话，双引号字符串就是 `java.lang.String` ， 但如果有插值存在的话，双引号字符串就是一个 `groovy.lang.GString` 实例。

为了转义一个双引号，你可以用反斜杠符号：\"A double quote: \\\"\".

### 字符串插值

除了单引号和三引号字符串,任何Groovy表达式都可以插值到所有的字符串里。 插值就是当计算字符串的值时，用值替换掉字符串中的占位符。

占位符表达式被 \${} 包围。在涉及到明确的点表达式时，大括号可以省略，也就是在这些情况下我们可以只使用 \$ 前缀。

当一个GString被传递到一个需要String参数的方法时, 在占位符中的表达式会被转换为他的字符串值 (通过在表达式上调用 `toString()`), 然后将整个String传递到方法中

在这里，我们有一个带占位符的字符串，这个占位符引用一个本地变量:

    def name = 'Guillaume' // a plain string
    def greeting = "Hello ${name}"
    
    assert greeting.toString() == 'Hello Guillaume'

然而任何的Groovy表达式都是有效的，正如我们看到的这个例子，它带有一个算术表达式:

    def sum = "The sum of 2 and 3 equals ${2 + 3}"
    assert sum.toString() == 'The sum of 2 and 3 equals 5'

不但表达式在\${}占位符里面是合法的。语句也同样是合法的，但语句的值为 `null` 。 所以如果有多个语句在占位符里面，最后的那个语句应该返回一个有意义的值，以便替换整个占位符的值。 例如， \"The sum of 1 and 2 is equal to \${def a = 1; def b = 2; a + b}\" 是可以工作的，但一个好的做法通常是在GString里面的占位符内的表达式应该尽量简单。

除了 \${} 占位符之外，我们还可以使用独立的 \$ 符号作为点表达式的前缀:

    def person = [name: 'Guillaume', age: 36]
    assert "$person.name is $person.age years old" == 'Guillaume is 36 years old'

但是，只有形式为 `a.b` 、`a.b.c` 等的点表达式是有效的。

Expressions containing parentheses like method calls, curly braces for closures, dots which aren't part of a property expression or arithmetic operators would be invalid.

给定如下的变量定义

    def number = 3.14

下面的句子会抛出一个 `groovy.lang.MissingPropertyException` 异常，因为Groovy认为你试图访问这个数字的 `toString` 这个不存在的属性：

    shouldFail(MissingPropertyException) {
        println "$number.toString()"
    }

你可以将 `"$number.toString()"` 想象为被解析器解释执行为 `"${number.toString}()"` .

类似地，如果表达式存在歧义，你需要保留大括号：

    String thing = 'treasure'
    assert 'The x-coordinate of the treasure is represented by treasure.x' ==
        "The x-coordinate of the $thing is represented by $thing.x"   // <= Not allowed: ambiguous!!
    assert 'The x-coordinate of the treasure is represented by treasure.x' ==
            "The x-coordinate of the $thing is represented by ${thing}.x"  // <= Curly braces required

如果需要在 GString 中转义 `$` 或 `${}` 占位符，使它们显示而不发生插值，只需要使用 `\` 反斜杠字符来转义美元符号：

    assert '$5' == "\$5"
    assert '${name}' == "\${name}"

### 插值闭包表达式的特殊情况

到目前为止，我们已经看到，我们可以插任意值到 `${}` 占位符，但这里有一个特殊情况就是闭包表达式。 当占位符包含一个箭头，`${→}` ,这个表达式事实上是一个闭包表达式 --- 你可以把它看作是一个带有美元符前缀的闭包：

    def sParameterLessClosure = "1 + 2 == ${-> 3}" 
    assert sParameterLessClosure == '1 + 2 == 3'
    
    def sOneParamClosure = "1 + 2 == ${ w -> w << 3}" 
    assert sOneParamClosure == '1 + 2 == 3'

-   这个闭包是一个不带参数的无参闭包。

-   在这里，闭包接受一个 `java.io.StringWriter` 参数，你可以用 `<<` 左移运算符添加内容。

在任一情况下，这两个占位符都是嵌套的闭包。

从表面上看，它看起来像是一种定义要插值的表达式的更详细的方式，但闭包比单纯的表达式有一个有趣的优点：惰性求值。

让我们考虑以下示例：

    def number = 1 
    def eagerGString = "value == ${number}"
    def lazyGString = "value == ${ -> number }"
    
    assert eagerGString == "value == 1" 
    assert lazyGString ==  "value == 1" 
    
    number = 2 
    assert eagerGString == "value == 1" 
    assert lazyGString ==  "value == 2" 

-   我们定义一个包含 1 的 number 变量，然后将其插入两个 GString 中，作为 eagerGString 中的表达式和 lazyGString 中的闭包。

-   我们期望结果字符串包含与 eagerGString 相同的字符串值 1。

-   对于 lazyGString 也是如此

-   然后我们将变量的值更改为新数字

-   对于简单的插值表达式，该值实际上是在创建 GString 时绑定的。

-   但使用闭包表达式时，每次将 GString 强制转换为 String 时都会调用闭包，从而生成包含新数值的更新字符串。

```{=html}
<!-- -->
```
-   带有多个参数的嵌入式闭包表达式将在运行时生成异常。只允许带有零个或一个参数的闭包。

### 与Java的互操作性

当一个方法（无论是用 Java 还是 Groovy 实现）需要一个 java.lang.String ，但我们传递一个 groovy.lang.GString 实例时，GString 的 toString() 方法会自动且透明地执行叫。

    String takeString(String message) {         
        assert message instanceof String        
        return message
    }
    
    def message = "The message is ${'hello'}"   
    assert message instanceof GString           
    
    def result = takeString(message)            
    assert result instanceof String
    assert result == 'The message is hello'

-   我们创建一个 GString 变量

-   我们仔细检查它是 GString 的实例

-   然后我们将该 GString 传递给以 String 作为参数的方法

-   takeString() 方法的签名明确表示其唯一参数是字符串

-   我们还验证该参数确实是一个 String 而不是 GString。

### GString和String的hashCodes

尽管可以使用插值字符串代替普通 Java 字符串，但它们在特定方面与字符串不同：它们的 hashCode 不同。普通 Java 字符串是不可变的，而 GString 的结果字符串表示形式可能会有所不同，具体取决于其插值。即使对于相同的结果字符串，GString 和 String 也不具有相同的 hashCode。

    assert "one: ${1}".hashCode() != "one: 1".hashCode()

GString 和 String 具有不同的 hashCode 值，应避免使用 GString 作为 Map 键，特别是当我们尝试使用 String 而不是 GString 检索关联值时。

    def key = "a"
    def m = ["${key}": "letter ${key}"]     
    
    assert m["a"] == null                   

-   该映射是使用一个初始对创建的，其键是 GString

-   当我们尝试使用 String 键获取值时，我们将找不到它，因为 String 和 GString 具有不同的 hashCode 值

## 三双引号字符串

三重双引号字符串的行为类似于双引号字符串，此外它们是多行的，就像三重单引号字符串一样。

    def name = 'Groovy'
    def template = """
        Dear Mr ${name},
    
        You're the winner of the lottery!
    
        Yours sincerly,
    
        Dave
    """
    
    assert template.toString().contains('Groovy')

双引号和单引号都不需要在三重双引号字符串中进行转义。

## 斜杠字符串

除了通常的带引号的字符串之外，Groovy 还提供斜杠字符串，它使用 / 作为开始和结束分隔符。斜杠字符串对于定义正则表达式和模式特别有用，因为不需要转义反斜杠。

斜杠字符串的示例：

    def fooPattern = /.*foo.*/
    assert fooPattern == '.*foo.*'

只有正斜杠需要用反斜杠转义：

    def escapeSlash = /The character \/ is a forward slash/
    assert escapeSlash == 'The character / is a forward slash'

斜杠字符串是多行的：

    def multilineSlashy = /one
        two
        three/
    
    assert multilineSlashy.contains('\n')

Slashy 字符串可以被认为是定义 GString 的另一种方式，但具有不同的转义规则。因此它们支持插值：

    def color = 'blue'
    def interpolatedSlashy = /a ${color} car/
    
    assert interpolatedSlashy == 'a blue car'

### 特殊情况

空的斜杠字符串不能用双正斜杠表示，因为 Groovy 解析器将其理解为行注释。这就是为什么以下断言实际上不会编译，因为它看起来像一个非终止语句：

    assert '' == //

由于斜线字符串主要是为了使正则表达式更容易而设计的，因此 GString 中的一些错误（例如 \$() 或 \$5 ）将适用于斜线字符串。

请记住，转义反斜杠不是必需的。另一种思考方式是，实际上不支持转义。斜杠字符串 `/\t/` 不会包含制表符，而是包含反斜杠，后跟字符"t"。仅允许转义正斜杠，即 `/\/folder/` 将是包含 `'/folder'` 的斜杠字符串。

转义正斜杠的结果是斜杠字符串不能以反斜杠结尾。否则将会转义斜杠字符串终止符。您可以使用特殊技巧 `/ends with slash ${'\'}/` 。但在这种情况下最好避免使用斜线。

## 美元符斜杠字符串

美元斜线字符串是用开头 \$/ 和结尾 /\$ 分隔的多行 GString。转义字符是美元符号，它可以转义另一个美元或正斜杠。仅当与美元和正斜杠字符的特殊使用发生冲突时才需要对这些字符进行转义。字符 \$foo 通常表示 GString 占位符，因此可以通过转义美元将这四个字符输入到美元斜线字符串中，即 \$\$foo 。同样，如果您希望美元斜线结束分隔符出现在字符串中，则需要对其进行转义。

这里有一些例子：

    def name = "Guillaume"
    def date = "April, 1st"
    
    def dollarSlashy = $/
        Hello $name,
        today we're ${date}.
    
        $ dollar sign
        $$ escaped dollar sign
        \ backslash
        / forward slash
        $/ escaped forward slash
        $$$/ escaped opening dollar slashy
        $/$$ escaped closing dollar slashy
    /$
    
    assert [
        'Guillaume',
        'April, 1st',
        '$ dollar sign',
        '$ escaped dollar sign',
        '\\ backslash',
        '/ forward slash',
        '/ escaped forward slash',
        '$/ escaped opening dollar slashy',
        '/$ escaped closing dollar slashy'
    ].every { dollarSlashy.contains(it) }

It was created to overcome some of the limitations of the slashy string escaping rules. Use it when its escaping rules suit your string contents (typically if it has some slashes you don't want to escape).

## 字符串汇总表

---------------- -------------- -------------- -------------- --------------
  字符串名称       字符串格式     是否支持插值   是否支持多行   转义字符

  单引号           `'…​'`          check empty    check empty    `\`

  三单引号         `'''…​'''`      check empty    check          `\`

  双引号           `"…​"`          check          check empty    `\`

  三双引号         `"""…​"""`      check          check          `\`

  斜杠字符串       `/…​/`          check          check          `\`

  美元斜杠字符串   `$/…​/$`        check          check          `$`
---------------- -------------- -------------- -------------- --------------

## 字符

与 Java 不同，Groovy 没有显式的字符文字。但是，您可以通过三种不同的方式明确将 Groovy 字符串设置为实际字符：

    char c1 = 'A' 
    assert c1 instanceof Character
    
    def c2 = 'B' as char 
    assert c2 instanceof Character
    
    def c3 = (char)'C' 
    assert c3 instanceof Character

-   通过在声明保存字符的变量时明确指定 char 类型

-   通过使用 as 运算符进行类型强制转换

-   通过使用强制转换为字符操作

# 数字

Groovy 支持不同类型的整数文字和十进制文字，由 Java Number 类型来实现。

## 整型字面量

整型类型与 Java 中的相同：

-   `byte`

-   `char`

-   `short`

-   `int`

-   `long`

-   `java.math.BigInteger`

您可以使用以下声明创建这些类型的整数：

    // primitive types
    byte  b = 1
    char  c = 2
    short s = 3
    int   i = 4
    long  l = 5
    
    // infinite precision
    BigInteger bi =  6

如果您通过使用 def 关键字来使用可选类型，则整数的类型将会有所不同：类型将根据数字的范围而变化

对于正数：

    def a = 1
    assert a instanceof Integer
    
    // Integer.MAX_VALUE
    def b = 2147483647
    assert b instanceof Integer
    
    // Integer.MAX_VALUE + 1
    def c = 2147483648
    assert c instanceof Long
    
    // Long.MAX_VALUE
    def d = 9223372036854775807
    assert d instanceof Long
    
    // Long.MAX_VALUE + 1
    def e = 9223372036854775808
    assert e instanceof BigInteger

对于负数也是如此：

    def na = -1
    assert na instanceof Integer
    
    // Integer.MIN_VALUE
    def nb = -2147483648
    assert nb instanceof Integer
    
    // Integer.MIN_VALUE - 1
    def nc = -2147483649
    assert nc instanceof Long
    
    // Long.MIN_VALUE
    def nd = -9223372036854775808
    assert nd instanceof Long
    
    // Long.MIN_VALUE - 1
    def ne = -9223372036854775809
    assert ne instanceof BigInteger

### 非十进制表示法

数字还可以用二进制、八进制、十六进制和小数表示。

#### 二进制

二进制数以 0b 前缀开头：

    int xInt = 0b10101111
    assert xInt == 175
    
    short xShort = 0b11001001
    assert xShort == 201 as short
    
    byte xByte = 0b11
    assert xByte == 3 as byte
    
    long xLong = 0b101101101101
    assert xLong == 2925l
    
    BigInteger xBigInteger = 0b111100100001
    assert xBigInteger == 3873g
    
    int xNegativeInt = -0b10101111
    assert xNegativeInt == -175

#### 八进制

八进制数以 0 后跟八进制数字的格式指定。

    int xInt = 077
    assert xInt == 63
    
    short xShort = 011
    assert xShort == 9 as short
    
    byte xByte = 032
    assert xByte == 26 as byte
    
    long xLong = 0246
    assert xLong == 166l
    
    BigInteger xBigInteger = 01111
    assert xBigInteger == 585g
    
    int xNegativeInt = -077
    assert xNegativeInt == -63

#### 十六进制

十六进制数字以 0x 后跟十六进制数字的格式指定。

    int xInt = 0x77
    assert xInt == 119
    
    short xShort = 0xaa
    assert xShort == 170 as short
    
    byte xByte = 0x3a
    assert xByte == 58 as byte
    
    long xLong = 0xffff
    assert xLong == 65535l
    
    BigInteger xBigInteger = 0xaaaa
    assert xBigInteger == 43690g
    
    Double xDouble = new Double('0x1.0p0')
    assert xDouble == 1.0d
    
    int xNegativeInt = -0x77
    assert xNegativeInt == -119

## 小数

小数类型与 Java 中的相同：

-   `float`

-   `double`

-   `java.math.BigDecimal`

您可以使用以下声明创建这些类型的小数：

    // primitive types
    float  f = 1.234
    double d = 2.345
    
    // infinite precision
    BigDecimal bd =  3.456

小数可以使用指数，其中 e 或 E 指数字母，后跟可选符号，以及代表指数的整数：

    assert 1e3  ==  1_000.0
    assert 2E4  == 20_000.0
    assert 3e+1 ==     30.0
    assert 4E-2 ==      0.04
    assert 5e-1 ==      0.5

为了方便精确的十进制数计算，Groovy 选择 `java.math.BigDecimal` 作为其十进制数类型。此外， `float` 和 `double` 均受支持，但需要显式类型声明、类型强制或后缀。即使 BigDecimal 是十进制数字的默认值，在采用 float 或 double 作为参数类型的方法或闭包中也会接受此类文字。

小数不能使用二进制、八进制或十六进制表示法来表示。

## 数字中的下划线

当编写长文字数字时，眼睛很难弄清楚某些数字是如何组合在一起的，例如数千个数字、单词等。通过允许您在数字文字中放置下划线，可以更容易地发现这些组：

    long creditCardNumber = 1234_5678_9012_3456L
    long socialSecurityNumbers = 999_99_9999L
    double monetaryAmount = 12_345_132.12
    long hexBytes = 0xFF_EC_DE_5E
    long hexWords = 0xFFEC_DE5E
    long maxLong = 0x7fff_ffff_ffff_ffffL
    long alsoMaxLong = 9_223_372_036_854_775_807L
    long bytes = 0b11010010_01101001_10010100_10010010

## 数字类型后缀

我们可以通过给出后缀（见下表）（大写或小写）来强制数字（包括二进制、八进制和十六进制）具有特定类型。

-----------------------------------------------------------------------
  Type                    Suffix
----------------------- -----------------------------------------------
  BigInteger              `G` or `g`

  Long                    `L` or `l`

  Integer                 `I` or `i`

  BigDecimal              `G` or `g`

  Double                  `D` or `d`

  Float                   `F` or `f`
  -----------------------------------------------------------------------

例子：

    assert 42I == Integer.valueOf('42')
    assert 42i == Integer.valueOf('42') // lowercase i more readable
    assert 123L == Long.valueOf("123") // uppercase L more readable
    assert 2147483648 == Long.valueOf('2147483648') // Long type used, value too large for an Integer
    assert 456G == new BigInteger('456')
    assert 456g == new BigInteger('456')
    assert 123.45 == new BigDecimal('123.45') // default BigDecimal type used
    assert .321 == new BigDecimal('.321')
    assert 1.200065D == Double.valueOf('1.200065')
    assert 1.234F == Float.valueOf('1.234')
    assert 1.23E23D == Double.valueOf('1.23E23')
    assert 0b1111L.class == Long // binary
    assert 0xFFi.class == Integer // hexadecimal
    assert 034G.class == BigInteger // octal

## 数学运算

尽管[操作符](core-operators.xml#groovy-operators)在其他地方有更详细的介绍，但讨论数学运算的行为及其结果类型很重要。

除了除法和幂运算（如下所述），

-   byte 、 char 、 short 和 int 之间的二元运算结果为 int

-   涉及 long 与 byte 、 char 、 short 和 int 的二元运算结果为 long

-   涉及 BigInteger 和任何其他整数类型的二元运算结果为 BigInteger

-   涉及 BigDecimal 与 byte 、 char 、 short 、 int 和 BigInteger 的二元运算结果为 BigDecimal

-   float 、 double 和 BigDecimal 之间的二元运算结果为 double

-   两个 BigDecimal 之间的二元运算结果为 BigDecimal

下表总结了这些规则：

----------------------------------------------------------------------------------------------------
                   byte    char    short   int     long    BigInteger   float    double   BigDecimal
---------------- ------- ------- ------- ------- ------- ------------ -------- -------- ------------
  **byte**         int     int     int     int     long    BigInteger   double   double   BigDecimal

  **char**                 int     int     int     long    BigInteger   double   double   BigDecimal

  **short**                        int     int     long    BigInteger   double   double   BigDecimal

  **int**                                  int     long    BigInteger   double   double   BigDecimal

  **long**                                         long    BigInteger   double   double   BigDecimal

  **BigInteger**                                           BigInteger   double   double   BigDecimal

  **float**                                                             double   double   double

  **double**                                                                     double   double

  **BigDecimal**                                                                          BigDecimal
  ----------------------------------------------------------------------------------------------------

由于 Groovy 的运算符重载，通常的算术运算符可以与 BigInteger 和 BigDecimal 一起使用，这与在 Java 中您必须使用显式方法来操作这些数字不同。

### 除法运算符

如果任一操作数是 float 或 double （以及用于除法和赋值的 /= ）会生成 double 结果 ，否则返回 BigDecimal 结果（当两个操作数都是整数类型 short 、 char 、 byte 的任意组合时、 int 、 long 、 BigInteger 或 BigDecimal ）。

如果除法是精确的（即产生可以在相同精度和小数位数的范围内表示的结果），则使用 divide() 方法执行 BigDecimal 除法，或者使用 MathContext [精度](http://docs.oracle.com/javase/7/docs/api/java/math/BigDecimal.html#precision())为两个操作数精度的最大值加上额外精度 10，http://docs.oracle.com/javase/7/docs/api/java/math/BigDecimal.html#scale()\[小数位数\]为最大值 10 和操作数小数位数的最大值。

对于 Java 中的整数除法，您应该使用 intdiv() 方法，因为 Groovy 没有提供专用的整数除法运算符符号。

### 幂运算

幂运算由 \*\* 运算符表示，有两个参数：底数和指数。幂运算的结果取决于其操作数和运算结果（特别是如果结果可以表示为整数值）。

Groovy 的幂运算使用以下规则来确定结果类型：

-   如果指数是小数

    -   如果结果可以表示为 Integer ，则返回 Integer

    -   否则，如果结果可以表示为 Long ，则返回 Long

    -   否则返回 Double

-   如果指数是整数值

    -   如果指数严格为负数，则返回 Integer 、 Long 或 Double （如果结果值符合该类型）

    -   如果指数为正或零

        -   如果基数是 BigDecimal ，则返回 BigDecimal 结果值

        -   如果基数是 BigInteger ，则返回 BigInteger 结果值

        -   如果基数是 Integer ，则如果结果值适合则返回 Integer ，否则返回 BigInteger

        -   如果基数是 Long ，如果结果值适合则返回 Long ，否则返回 BigInteger

我们可以用几个例子来说明这些规则：

    // base and exponent are ints and the result can be represented by an Integer
    assert    2    **   3    instanceof Integer    //  8
    assert   10    **   9    instanceof Integer    //  1_000_000_000
    
    // the base is a long, so fit the result in a Long
    // (although it could have fit in an Integer)
    assert    5L   **   2    instanceof Long       //  25
    
    // the result can't be represented as an Integer or Long, so return a BigInteger
    assert  100    **  10    instanceof BigInteger //  10e20
    assert 1234    ** 123    instanceof BigInteger //  170515806212727042875...
    
    // the base is a BigDecimal and the exponent a negative int
    // but the result can be represented as an Integer
    assert    0.5  **  -2    instanceof Integer    //  4
    
    // the base is an int, and the exponent a negative float
    // but again, the result can be represented as an Integer
    assert    1    **  -0.3f instanceof Integer    //  1
    
    // the base is an int, and the exponent a negative int
    // but the result will be calculated as a Double
    // (both base and exponent are actually converted to doubles)
    assert   10    **  -1    instanceof Double     //  0.1
    
    // the base is a BigDecimal, and the exponent is an int, so return a BigDecimal
    assert    1.2  **  10    instanceof BigDecimal //  6.1917364224
    
    // the base is a float or double, and the exponent is an int
    // but the result can only be represented as a Double value
    assert    3.4f **   5    instanceof Double     //  454.35430372146965
    assert    5.6d **   2    instanceof Double     //  31.359999999999996
    
    // the exponent is a decimal value
    // and the result can only be represented as a Double value
    assert    7.8  **   1.9  instanceof Double     //  49.542708423868476
    assert    2    **   0.1f instanceof Double     //  1.0717734636432956

# 布尔类型

布尔值是一种特殊的数据类型，用于表示真假： true 和 false 。将此数据类型用于表示真/假[条件](core-operators.xml#Operator-Overloading)的简单标志。

布尔值可以存储在变量中，分配到字段中，就像任何其他数据类型一样：

    def myBooleanVariable = true
    boolean untypedBooleanVar = false
    booleanField = true

true 和 false 是仅有的两个原始布尔值。但更复杂的布尔表达式可以使用[逻辑运算符](core-operators.xml#logical_operators)来表示。

此外，Groovy 有特殊规则（通常称为 Groovy Truth）用于将非布尔对象强制为布尔值。

# Lists

Groovy 使用逗号分隔的值列表（用方括号括起来）来表示列表。 Groovy 列表是普通的 JDK java.util.List ，因为 Groovy 没有定义自己的集合类。定义列表文字时使用的具体列表实现默认为 java.util.ArrayList ，除非您决定另外指定，我们稍后会看到。

    def numbers = [1, 2, 3]         
    
    assert numbers instanceof List  
    assert numbers.size() == 3      

-   我们定义一个由逗号分隔并用方括号括起来的列表数字，并将该列表分配给一个变量

-   该列表是 Java 的 java.util.List 接口的实例

-   可以使用 size() 方法查询列表的大小，并显示我们的列表包含 3 个元素

在上面的示例中，我们使用了元素类型相同的列表，但您也可以创建包含异构类型的列表：

    def heterogeneous = [1, "a", true]  

-   我们这里的列表包含一个数字、一个字符串和一个布尔值

我们提到，**默认情况下，列表文字实际上是** `java.util.ArrayList` **的实例，但由于使用** `as` **运算符的类型强制，可以为列表使用不同的支持类型，或者使用变量的显式类型声明：**

    def arrayList = [1, 2, 3]
    assert arrayList instanceof java.util.ArrayList
    
    def linkedList = [2, 3, 4] as LinkedList    
    assert linkedList instanceof java.util.LinkedList
    
    LinkedList otherLinked = [3, 4, 5]          
    assert otherLinked instanceof java.util.LinkedList

-   我们使用带有 as 运算符的强制来显式请求 java.util.LinkedList 实现

-   保存列表文字的变量是 java.util.LinkedList 类型

您可以使用 \[\] 下标运算符（用于读取和设置值）使用正索引或负索引访问列表的元素，以访问列表末尾的元素以及范围，并使用 \<\< leftShift 运算符将元素追加到列表中：

    def letters = ['a', 'b', 'c', 'd']
    
    assert letters[0] == 'a'     
    assert letters[1] == 'b'
    
    assert letters[-1] == 'd'    
    assert letters[-2] == 'c'
    
    letters[2] = 'C'             
    assert letters[2] == 'C'
    
    letters << 'e'               
    assert letters[ 4] == 'e'
    assert letters[-1] == 'e'
    
    assert letters[1, 3] == ['b', 'd']         
    assert letters[2..4] == ['C', 'd', 'e']    

-   访问列表的第一个元素（从零开始计数）

-   使用负索引访问列表的最后一个元素：-1 是列表末尾的第一个元素

-   使用赋值为列表的第三个元素设置新值

-   使用 \<\< leftShift 运算符将元素追加到列表末尾

-   一次访问两个元素，返回包含这两个元素的新列表

-   使用范围访问列表中从开始到结束元素位置的一系列值

由于列表本质上可以是异构(heterogeneous )的，因此列表还可以包含其他列表来创建多维列表：

    def multi = [[0, 1], [2, 3]]     
    assert multi[1][0] == 2          

-   定义数字列表

-   访问最顶层列表的第二个元素和内部列表的第一个元素

# Arrays

**Groovy 使用了 List 的表示法来表示数组，但是要创建数组，您需要通过强制或类型声明来显式定义数组的类型。**

    String[] arrStr = ['Ananas', 'Banana', 'Kiwi']  
    
    assert arrStr instanceof String[]    
    assert !(arrStr instanceof List)
    
    def numArr = [1, 2, 3] as int[]      
    
    assert numArr instanceof int[]       
    assert numArr.size() == 3

-   使用显式变量类型声明定义字符串数组

-   断言我们创建了一个字符串数组

-   使用 as 运算符创建一个整数数组

-   断言我们创建了一个原始整数数组

您还可以创建多维数组：

    def matrix3 = new Integer[3][3]         
    assert matrix3.size() == 3
    
    Integer[][] matrix2                     
    matrix2 = [[1, 2], [3, 4]]
    assert matrix2 instanceof Integer[][]

-   您可以定义新数组的边界

-   或者声明一个数组而不指定其边界

对数组元素的访问遵循与列表相同的表示法：

    String[] names = ['Cédric', 'Guillaume', 'Jochen', 'Paul']
    assert names[0] == 'Cédric'     
    
    names[2] = 'Blackdrag'          
    assert names[2] == 'Blackdrag'

-   检索数组的第一个元素

-   将数组第三个元素的值设置为新值

## Java风格的数组初始化

Groovy 始终支持使用方括号的列表/数组定义，并避免使用 Java 风格的大括号，以免与闭包定义冲突。然而，如果大括号紧跟在数组类型声明之后，则闭包定义不会产生歧义，因此 Groovy 3 及更高版本支持 Java 数组初始化表达式。

Examples:

    def primes = new int[] {2, 3, 5, 7, 11}
    assert primes.size() == 5 && primes.sum() == 28
    assert primes.class.name == '[I'
    
    def pets = new String[] {'cat', 'dog'}
    assert pets.size() == 2 && pets.sum() == 'catdog'
    assert pets.class.name == '[Ljava.lang.String;'
    
    // traditional Groovy alternative still supported
    String[] groovyBooks = [ 'Groovy in Action', 'Making Java Groovy' ]
    assert groovyBooks.every{ it.contains('Groovy') }

# Maps

在其他语言中有时称为字典或关联数组，它具有映射功能。

映射将键与值关联起来，用冒号分隔键和值，每个键/值对用逗号分隔，整个键和值用方括号括起来。

    def colors = [red: '#FF0000', green: '#00FF00', blue: '#0000FF']   
    
    assert colors['red'] == '#FF0000'    
    assert colors.green  == '#00FF00'    
    
    colors['pink'] = '#FF00FF'           
    colors.yellow  = '#FFFF00'           
    
    assert colors.pink == '#FF00FF'
    assert colors['yellow'] == '#FFFF00'
    
    assert colors instanceof java.util.LinkedHashMap

-   我们定义一个字符串颜色名称的映射，与其十六进制编码的 html 颜色相关联

-   我们使用下标表示法来检查与 red 键关联的内容

-   我们还可以使用属性符号来断言绿色的十六进制表示

-   类似地，我们可以使用下标符号来添加新的键/值对

-   或者通过属性符号，添加 yellow 颜色

When using names for the keys, we actually define string keys in the map.

Groovy 创建的map实际上是 `java.util.LinkedHashMap` 的实例。

如果您尝试访问map中不存在的key：

    assert colors.unknown == null
    
    def emptyMap = [:]
    assert emptyMap.anyKey == null

您将获得 null 。

在上面的示例中，我们使用了字符串作为key，但您也可以使用其他类型的值作为键：

    def numbers = [1: 'one', 2: 'two']
    
    assert numbers[1] == 'one'

在这里，我们使用数字作为键，因为数字可以明确地被识别为数字，因此 Groovy 不会像我们前面的示例中那样创建字符串键。但考虑一下您想要传递变量代替键的情况，以使该变量的值成为键：

    def key = 'name'
    def person = [key: 'Guillaume']      
    
    assert !person.containsKey('name')   
    assert person.containsKey('key')     

-   与 \'Guillaume\' 名称关联的 key 实际上是 \"key\" 字符串，而不是与 key 变量关联的值

-   map不包含 \'name\' 键

-   相反，map包含一个 \'key\' 键

您还可以传递带引号的字符串和键：\["name"："Guillaume"\]。如果您的密钥字符串不是有效的标识符，则这是强制性的，例如，如果您想创建一个包含破折号的字符串密钥，如：\["street-name"："Main street"\]。

当您需要将变量值作为映射定义中的键传递时，必须用括号将变量或表达式括起来：

    person = [(key): 'Guillaume']        
    
    assert person.containsKey('name')    
    assert !person.containsKey('key')    

-   这次，我们用括号将 key 变量括起来，以指示解析器我们传递变量而不是定义字符串键

-   该map确实包含 name 键

-   但map不像以前那样包含 key 键
